Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Indomitable statuses
DB_Talents_IndomitableStatuses("CHICKEN");
DB_Talents_IndomitableStatuses("FROZEN");
DB_Talents_IndomitableStatuses("PETRIFIED");
DB_Talents_IndomitableStatuses("STUNNED");
DB_Talents_IndomitableStatuses("KNOCKED_DOWN");
DB_Talents_IndomitableStatuses("CRIPPLED");

//Cooldown statuses (frozen with duration if character dies)
DB_Talents_Cooldowns("INDOMITABLE_CD");
DB_Talents_Cooldowns("JITTERBUG_CD");
DB_Talents_Cooldowns("GLADIATOR_CD");

//Magic Cycles
DB_Talents_Cycles("CYCLE_FIRE_WATER", "CYCLE_EARTH_AIR");
DB_Talents_Cycles("CYCLE_EARTH_AIR", "CYCLE_FIRE_WATER");
KBSECTION
//REGION Re-apply cooldown statuses after res.
IF
CharacterVitalityChanged(_Char, _Per)
AND
_Per == 0
AND
DB_Talents_Cooldowns(_Status)
AND
HasActiveStatus(_Char, _Status, 1)
AND
GetStatusTurns(_Char, _Status, _Duration)
AND
IntegertoString(_Duration, _Str)
THEN
DebugBreak("PROCCING ON DEATH");
DebugBreak(_Str);
DB_Talents_DeadCooldowns(_Char, _Status, _Duration);

IF
CharacterResurrected(_Char)
AND
DB_Talents_DeadCooldowns(_Char, _Status, _Duration)
AND
Real(_Duration, _DurationR)
AND
RealProduct(_DurationR, 6.0, _Dur)
THEN
ApplyStatus(_Char, _Status, _Dur);
NOT DB_Talents_DeadCooldowns(_Char, _Status, _Duration);

//END_REGION
//REGION Indomitable
IF
CharacterStatusApplied(_Char, _Status, _)
AND
CharacterHasTalent(_Char, "Indomitable", 1)
AND
DB_Talents_IndomitableStatuses(_Status)
AND
HasActiveStatus(_Char, "INDOMITABLE_CD", 0)
THEN
DebugBreak("INDOMITABLE LANUCHED");
ApplyStatus(_Char, "INDOMITABLE", 6.0);

IF
CharacterStatusRemoved(_Char, "INDOMITABLE", _)
THEN
ApplyStatus(_Char, "INDOMITABLE_CD", 12.0);

//indomitable knockdown removal fix
IF
CharacterStatusAttempt(_Char, "INDOMITABLE", _)
AND
HasActiveStatus(_Char, "KNOCKED_DOWN", 1)
THEN
RemoveStatus(_Char, "KNOCKED_DOWN");


//END_REGION

//REGION Magic Cycles
IF
ObjectEnteredCombat(_Char, _)
AND
CharacterHasTalent((CHARACTERGUID)_Char, "MagicCycles", 1)
AND
DB_IsPlayer((CHARACTERGUID)_Char)
AND
Random(2, _Rand)
//add talent check
AND
NOT QRY_CMP_Talents_CyclesStatusCheck((GUIDSTRING)_Char)
THEN
PROC_CMP_Talents_CycleApplyStatusOnEnter(_Rand, _Char);

PROC
PROC_CMP_Talents_CycleApplyStatusOnEnter((INTEGER)_Rand, (GUIDSTRING)_Char)
AND
_Rand == 0
THEN
ApplyStatus((GUIDSTRING)_Char, "CYCLE_FIRE_WATER", 6.0);

PROC
PROC_CMP_Talents_CycleApplyStatusOnEnter((INTEGER)_Rand, (GUIDSTRING)_Char)
AND
_Rand == 1
THEN
ApplyStatus((GUIDSTRING)_Char, "CYCLE_EARTH_AIR", 6.0);

IF
CharacterStatusRemoved(_Char, _Status, _)
//add talent check
AND
DB_Talents_Cycles(_Status, _Status2)
AND
CharacterIsInCombat(_Char, 1)
AND
CharacterIsDead(_Char, 0)
THEN
ApplyStatus((GUIDSTRING)_Char, _Status2, 6.0);

QRY
QRY_CMP_Talents_CyclesStatusCheck((GUIDSTRING)_Char)
AND
DB_Talents_Cycles(_Status, _)
AND
HasActiveStatus(_Char, _Status, 1)
THEN
DB_NOOP(1);
//END_REGION 

//REGION Master Chef

IF
CharacterPickpocketEnter(_Character, _Target)
AND
CharacterHasTalent((CHARACTERGUID)_Character, "MasterThief", 1)
THEN
Proc_CMP_CheckCannotPickpocketTag((CHARACTERGUID) _Target);
Proc_CMP_ApplyInvisibleStatus((CHARACTERGUID) _Character);
Proc_CMP_ClearCannotPickpocketDB();
DB_CMP_MasterThiefPickpocketer(_Character);

PROC
Proc_CMP_CheckCannotPickpocketTag((CHARACTERGUID) _Target)
AND
DB_CannotPickpocketTags(_Tag)
AND
IsTagged(_Target, _Tag, 1)
THEN
DB_CMP_CannotPickpocket(1);

PROC
Proc_CMP_ApplyInvisibleStatus((CHARACTERGUID) _Character)
AND
NOT DB_CMP_CannotPickpocket(1)
THEN
ApplyStatus(_Character, "INVISIBLE", -1.0, 1, _Character);

PROC
Proc_CMP_ClearCannotPickpocketDB()
AND
DB_CMP_CannotPickpocket(1)
THEN
NOT DB_CMP_CannotPickpocket(1);

IF
SavegameLoaded(_, _, _, _)
AND
DB_CMP_MasterThiefPickpocketer(_Character)
THEN
RemoveStatus(_Character, "INVISIBLE");
NOT DB_CMP_MasterThiefPickpocketer(_Character);


IF
CharacterPickpocketExit(_Character)
AND
CharacterHasTalent((CHARACTERGUID)_Character, "MasterThief", 1)
THEN
RemoveStatus(_Character, "INVISIBLE");
NOT DB_CMP_MasterThiefPickpocketer(_Character);

//END_REGION

//REGION Jitterbug
IF
CharacterReceivedDamage(_Defender, _Perc, _Attacker)
//AttackedByObject(_Defender, _, _Attacker, _, _StatusEnter)
AND
_Perc > 0
AND
QRY_CMP_Jitterbug_HasArmor(_Defender)
AND
HasAppliedStatus(_Defender, "JITTERBUG_CD", 0)
AND
CharacterHasTalent((CHARACTERGUID)_Defender, "Jitterbug", 1)
THEN
ApplyStatus(_Defender, "JITTERBUG_CD", 12.0);
PlayEffect(_Defender, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
CharacterJitterbugTeleport((CHARACTERGUID)_Defender,(CHARACTERGUID)_Attacker, 8.0, 9.0);

QRY
QRY_CMP_Jitterbug_HasArmor((GUIDSTRING)_Defender)
AND
CharacterGetArmorPercentage((CHARACTERGUID)_Defender, _Percentage)
AND
_Percentage == 0
THEN
DB_NOOP(1);

QRY
QRY_CMP_Jitterbug_HasArmor((GUIDSTRING)_Defender)
AND
CharacterGetMagicArmorPercentage((CHARACTERGUID)_Defender, _Percentage)
AND
_Percentage == 0
THEN
DB_NOOP(1);
//END_REGION

//REGION Soulcatcher
IF
CharacterDied(_Char)
AND
DB_IsPlayer(_Char)
THEN
PROC_CMP_Soulcatcher_IsWithinSoulcatcherRange(_Char);


PROC
PROC_CMP_Soulcatcher_IsWithinSoulcatcherRange((CHARACTERGUID)_DeceasedChar)
AND
DB_IsPlayer(_SoulCatcherChar)
AND
CharacterIsInPartyWith(_DeceasedChar, _SoulCatcherChar, 1)
AND
CharacterHasTalent((CHARACTERGUID)_SoulCatcherChar, "Soulcatcher", 1)
AND
CharacterIsDead(_SoulCatcherChar, 0)
AND
GetDistanceTo(_DeceasedChar, _SoulCatcherChar, _Dist)
AND
_Dist <= 12.0
AND
CharacterIsEnemy(_SoulCatcherChar, _DeceasedChar, 0)
AND
GetPosition(_DeceasedChar, _X, _Y, _Z)
AND
CharacterGetAbility(_SoulCatcherChar, "Summoning", _SummoningAbilityLevel)
AND
CharacterSummonAtPosition(_DeceasedChar, "e7e89c3f-b491-4771-a2e2-602cc89c3631", _X, _Y, _Z, 18.0, -1, _SummoningAbilityLevel, _SummonedChar)
AND
CharacterGetLevel(_DeceasedChar, _Level)
AND
IntegertoString(_Level, _Str)
THEN
DB_CMP_Soulcatcher(_DeceasedChar, _SummonedChar, 1);
PROC_CMP_Soulcatcher_AddSkills(_SummonedChar, _Level);

PROC
PROC_CMP_Soulcatcher_AddSkills((CHARACTERGUID)_Spawn, (INTEGER)_Level)
AND
_Level >= 9
THEN
CharacterAddSkill(_Spawn, "Shout_EnemyPoisonWave", 1);

PROC
PROC_CMP_Soulcatcher_AddSkills((CHARACTERGUID)_Spawn, (INTEGER)_Level)
AND
_Level >= 16
THEN
CharacterAddSkill(_Spawn, "Shout_EnemySiphonPoison", 1);

IF
CharacterResurrected(_Char)
AND
DB_CMP_Soulcatcher(_Char, _Spawn, _Int)
THEN
NOT DB_CMP_Soulcatcher(_Char, _Spawn, _Int);
CharacterDie((CHARACTERGUID)_Spawn, 0, "DoT");

IF
CharacterStatusRemoved(_Spawn, "SUMMONING_ABILITY", _)
AND
DB_CMP_Soulcatcher(_Char, _Spawn, _Int)
THEN
NOT DB_CMP_Soulcatcher(_Char, _Spawn, _Int);
CharacterDie((CHARACTERGUID)_Spawn, 0, "DoT");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
